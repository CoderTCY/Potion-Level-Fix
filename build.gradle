plugins {
    id 'idea'
    id 'net.neoforged.moddev.legacyforge' version '2.0.88'
}

group = mod_group_id
version = mod_version

base {
    archivesName = "${mod_name}-${modloader}-${minecraft_version}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}
legacyForge {
    version = "${minecraft_version}-${forge_version}"


    if (mapping_channel=="parchment"){
        parchment {
            minecraftVersion = '1.20.1'
            mappingsVersion = '2023.09.03'
        }
    }

    runs {
        client {
            client()
        }
        data {
            data()
        }
        server {
            server()
            setGameDirectory(file("server"))
        }
        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            jvmArgument "-XX:+IgnoreUnrecognizedVMOptions"
            jvmArgument "-XX:+AllowEnhancedClassRedefinition"
            jvmArgument "-XX:HotswapAgent=fatjar"

            logLevel = org.slf4j.event.Level.DEBUG
            if (isRunningInIdea()) {
                systemProperty "terminal.jline", "true"
            }
        }
    }

    mods {
        testproject {
            sourceSet sourceSets.main
        }
    }
}

repositories {
    maven { url = "https://maven.theillusivec4.top/" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url 'https://jitpack.io' }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
//    modImplementation("curse.maven:attributefix-280510:4911084")
    modCompileOnly("curse.maven:jade-324717:${jade_version}")
    modRuntimeOnly("curse.maven:jade-324717:${jade_version}")
    
    compileOnly 'org.jetbrains:annotations:24.0.1'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0"))
    modImplementation(jarJar("io.github.llamalad7:mixinextras-forge:0.5.0"))
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "mixins.${mod_id}.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

processResources {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description,
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }}

jar {
    manifest.attributes([
            "Specification-Title": mod_id,
            "Specification-Vendor": mod_authors,
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": project.jar.archiveVersion,
            "Implementation-Vendor": mod_authors,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "mixins.${mod_id}.json"
    ])
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

def isRunningInIdea() {
    return System.getProperty("idea.active") == "true"
}
