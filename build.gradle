plugins {
    id "dev.architectury.loom" version "1.4-SNAPSHOT"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "8.1.0"
}

group = mod_group_id
version = mod_version

base {
    archivesName = "${mod_name}-${loom.platform.get().id()}-${minecraft_version}"
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

loom {
    silentMojangMappingsLicense()

    forge {
        mixinConfigs = [
                "mixins.${mod_id}.json"
        ]
    }
    mixin.defaultRefmapName.set("${mod_id}.refmap.json")
}

repositories {
    maven { url = "https://maven.theillusivec4.top/" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url 'https://jitpack.io' }
    maven {
        url "https://cursemaven.com"
    }
}

configurations {
    implementation.extendsFrom shadow
}

dependencies {
//    modImplementation("curse.maven:attributefix-280510:4911084")
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings loom.officialMojangMappings()
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"


    modCompileOnly("curse.maven:jade-324717:${jade_version}")
    modRuntimeOnly("curse.maven:jade-324717:${jade_version}")

    shadow(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0"))
//    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0"))
//    forgeRuntimeLibrary(include("io.github.llamalad7:mixinextras-common:0.5.0"))
}

shadowJar {
    configurations = [project.configurations.shadow]
    relocate("com.llamalad7.mixinextras", "com.teampotato.potion_level_fix.mixinextras")
    mergeServiceFiles()
}

remapJar {
    dependsOn tasks.named("shadowJar")
    inputFile.set(tasks.named("shadowJar").map { it.archiveFile.get() })
}

def generatedResources = file("src/generated")
sourceSets.main.resources { srcDir generatedResources }

processResources {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description,
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

java {
    withSourcesJar()
}

jar {
    manifest.attributes([
            "Implementation-Title": project.name,
            "Implementation-Version": project.jar.archiveVersion,
            "Implementation-Vendor": mod_authors,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
    ])
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release = targetVersion
    }
}

def isRunningInIdea() {
    return System.getProperty("idea.active") == "true"
}